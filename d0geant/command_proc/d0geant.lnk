$!------------------------------------------------
$!
$! Name      : D0GEANT.LNK
$!
$! Purpose   : Link D0GEANT.EXE
$!
$! Arguments : P1 - "DEBUG" to create DEB_'TASK'D0GEANT.EXE
$!                - "PCA"   to create DEB_'TASK'D0GEANT.EXE with PCA
$!                      (DECs Program Coverage Analyser)
$!
$! Created  20-APR-1989   A.M.Jonckheere
$!
$!------------------------------------------------
$   ON ERROR     Then Goto EXIT
$   ON CONTROL_Y Then Goto EXIT
$!
$ IF "''TASK'" .eqs. ""
$ THEN
$     TASK = "FULLD0"
$     pgbd_sources_object = "'DEB'FULLD0_D0GEANT.OBJ,"
$     user_libraries = ""
$ ENDIF
$!
$ fblen = f$locate("]",pgbd_sources_object)
$ fslen = f$length(pgbd_sources_object)
$ if fblen .ne. fslen 
$ then
$   source = f$extract(fblen+1,fslen-fblen,pgbd_sources_object)
$ else
$   fblen = f$locate(":",pgbd_sources_object)
$   if fblen .ne. fslen 
$   then
$     source = f$extract(fblen+1,fslen-fblen,pgbd_sources_object)
$   else
$     source = pgbd_sources_object
$   endif
$ endif
$ pgbd_sources_object = source
$ if "''defdir'" .nes. ""
$ then
$   define/nolog def_sea [],'defdir,d0$d0geant:
$ else
$   define/nolog def_sea [],d0$d0geant:
$ endif
$ pgbd_sources  = "def_sea:''pgbd_sources_object'"                              
$!                                                                              
$!
$ IF P1 .eqs "DEBUG"
$ THEN
$   SWIT  = "/DEBUG"
$   FORSW = "/DEBUG=ALL/NOOPT"
$   DEB   =  "DEB_"
$ ELSE
$     SWIT  = ""
$     FORSW = ""
$     DEB   = ""
$ ENDIF
$ IF P1 .eqs. "PCA"
$ THEN
$   SWIT  = "/DEBUG=SYS$LIBRARY:PCA$OBJ.OBJ"
$   FORSW = "/DEBUG=ALL/NOOPT"
$   DEB   = "DEB_"
$ ENDIF
$!
$!.....        check D0NEW.OPT
$!
$ IF "''linkopt'" .eqs. ""
$ THEN
$     linkopt = "D0NEW.OPT"
$ ENDIF
$ optfile = f$search("''linkopt'")
$ IF optfile .eqs. ""
$ THEN
$     Write Sys$output "        No option file ''linkopt', will link without"
$     D0NEW = ""
$ ELSE
$     D0NEW = "''linkopt'/Opt,"
$ ENDIF
$!
$   COPY SYS$INPUT SYS$OUTPUT

 *********************************************************************
 D0Geant is now a full Program_Builder Program. To change the sizes of 
 the three (3) main ZEBRA stores in D0GEANT you must run Program_Builder.

   PBD/FRAME=D0GEANT/NAME=your_name-
        /PACKAGES=(D0,GVTX,GCDC,GFDC,GTRD,GLV0,GCAL,GMUO,GSAM)-
                /ZEBCOM=iii/ZEBSTP=jjj/ZEBWRK=kkk/PAWC=lll/GCBANK=mmm

        NOTE: D0 *must* always be declared, and must be first.

 Files your_name_D0GEANT.COM, .LNK, .FOR and the DEB_ and non-DEB_ OBJ's
 will be created in your area. @your_name_D0GEANT.COM should be used to setup
 D0Geant's logicals. @your_name_D0GEANT.LNK [DEBUG] will link
 your_name_D0GEANT.EXE.

 The default sizes in FULLD0_COMBINED.PBD (FULLD0_D0GEANT.EXE) are:
   ZEBCOM=1.75, ZEBSTP=0.60, ZEBWRK=0.02, PAWC=0.25, GCBANK=1.5 Mwd

 *********************************************************************

$!
$   WRITE SYS$OUTPUT "Linking ''DEB'''TASK'_D0Geant"
$   WRITE SYS$OUTPUT " "
$  LINK'SWIT'/NOMAP/EXE='DEB''TASK'_D0GEANT -
        'D0NEW' -
        'pgbd_sources' -
        'user_libraries' -
        D0$D0GEANT:'DEB'D0GEANT/OPT
$   WRITE SYS$OUTPUT "Link done"
$   WRITE SYS$OUTPUT " "
$!
$EXIT:
$   EXIT
