      SUBROUTINE XLUM(XM,YM,ZM,CTM,STM,CTPHM,STPHM,EX,X,Y,Z)
C----------------------------------------------------------------------
C-
C-   Purpose and Methods : 
C  ANALYSE THE X RAY RESULTING FROM LUMINESCENCE OF TRANSITION X RAYS
C-
C-   Inputs  : XM,YM,ZM,CTM,STM,CTPHM,STPHM= KINEMATICS VARIABLES FOR
C-             THE MOTHER PARTICLE
C-   Outputs : X,Y,Z,EX= KINEMATICS VARIABLES FOR THE HIGH ENERGY X RAY
C-   Controls: 
C-
C-   Created                A. ZYLBERSTEJN
C-   Updated  19-SEP-1988   J.R. HUBBARD/A. ZYLBERSTEJN   
C-
C----------------------------------------------------------------------
C
      IMPLICIT NONE
      INCLUDE 'D0$INC:ABSOR.INC/LIST'
      INCLUDE 'D0$INC:GCUNIT.INC/LIST'
      INCLUDE 'D0$INC:GCONST.INC'
      INCLUDE 'D0$INC:D0LOG.INC/LIST'
C
C
      REAL XM,YM,ZM,CTM,STM,CTPHM,STPHM,EX,X,Y,Z
      REAL XABS,VINT,CT,PH,ST,CPH,SPH,XX,YX,ZX,APHASA,RNDM,XRND
      INTEGER I,IFOIS,IPRNT
      DATA IFOIS/0/
      IFOIS=IFOIS+1
      IPRNT=0
      IF(IFOIS.LE.10 .AND. PTRD.GE.5)IPRNT=1
C   CHOOSE AN INTERACTION POINT
      CALL INTERP(EABS,ABGAS,NABS,EX,XABS)
      VINT=-ALOG(1.-RNDM(XRND))/XABS
C  CHOOSE A DIRECTION IN THE MOTHER PARTICLE SYSTEM
      CT=APHASA(-1.,1.)
      PH=TWOPI*RNDM(0)
      ST=SQRT(1.-CT**2)
      SPH=SIN(PH)
      CPH=COS(PH)
      XX=VINT*ST*CPH
      YX=VINT*ST*SPH
      ZX=VINT*CT
C  MAKE THE ROTATION TO GO BACK TO THE GENERAL SYSTEM
      X=XM+CTPHM*(CTM*XX+STM*ZX)-STPHM*YX
      Y=YM+STPHM*(CTM*XX+STM*ZX)+CTPHM*YX
      Z=ZM-STM*XX+CTM*ZX
C
      IF (IPRNT.EQ.1) THEN
        WRITE(LOUT,*)' IN XLUM.  EX',EX,' XABS',XABS,' VINT',VINT
        WRITE(LOUT,*)'     X,Y,Z IN  : ',XM,YM,ZM
        WRITE(LOUT,*)'     X,Y,Z OUT : ',X,Y,Z
        WRITE(LOUT,*)'     DX,DY,DZ  : ',XX,YX,ZX
        WRITE(LOUT,*)'EABS'
        WRITE(LOUT,*)(EABS(I),I=1,NABS)
        WRITE(LOUT,*)'ABGAS'
        WRITE(LOUT,*)(ABGAS(I),I=1,NABS)
      ENDIF
      RETURN
      END
