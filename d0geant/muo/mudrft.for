      REAL FUNCTION MUDRFT(VECLOC,HEIGHT)  
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
CCC 
CC   CALCULATES MUON DRIFT TIME 
CC   INPUTS ARE LOCAL VECTOR X,Y,Z,DX,DY,DZ 
CC   X IS HEIGHT  , Z ALONG WIRE, Y LENGTH (10 CM)
CC  
CC   11-1-85  HEDIN 
CC  S.KUNORI   2-APR-87    MODIFIED.
CC    modification by SK.
CC  (1) replace vecloc(3),(6) by (2),(5).
CC        :     z1,z2 by y1,y2
CC        :     width by height
CC  (2) height was added in input argument.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
CC  
      IMPLICIT NONE
      INTRINSIC SQRT,ABS
      REAL SPEED,DRIFT,HEIGHT,DEL,SINT,COST,Y1,Y2
      REAL VECLOC(6)    
      DATA SPEED/200./    ! DRIFT SPEED, CELL HALF HEIGHT
CCCC     FIND DISTANCE OF CLOSEST APPROACH
CCCC     ASSUME CONSTANT DRIFT VELOCITY
      MUDRFT=1200.  
      IF(VECLOC(4).NE.0.) THEN  
        DEL=VECLOC(2)-VECLOC(1)*VECLOC(5)/VECLOC(4) ! DISTANCE AT MIDPOINT
        SINT=VECLOC(5)/SQRT(VECLOC(4)**2+VECLOC(5)**2)  
        COST=VECLOC(4)/SQRT(VECLOC(4)**2+VECLOC(5)**2)
        DRIFT=ABS(DEL*COST)
        IF(DRIFT*ABS(SINT).GT.HEIGHT) THEN       ! EXCEED CRITICAL ANGLE
          Y1=VECLOC(2)+(HEIGHT-VECLOC(1))*VECLOC(5)/VECLOC(4)
          Y2=VECLOC(2)+(-HEIGHT-VECLOC(1))*VECLOC(5)/VECLOC(4)
CC   SEE WHICH OF TWO BOUNDARY SOLUTIONS IS CLOSEST
          IF(ABS(Y1).LT.ABS(Y2)) THEN
            DRIFT=SQRT(HEIGHT**2+Y1**2)
          ELSE
            DRIFT=SQRT(HEIGHT**2+Y2**2)
          ENDIF
        ENDIF
        MUDRFT=DRIFT*SPEED
      ENDIF 
      RETURN    
      END   
