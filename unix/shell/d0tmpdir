#! /bin/csh -f
###############################################################################
#
# Name:  d0tmpdir
#
# Purpose: Generate a unique temporary directory name.
#
# Usage:
#
# % d0tmpdir [-c] [<dir>]
#
# Options:
#
# -c    - Clean up temporary area as a side effect.
#
# Arguments:
#
# <dir> - An optional argument specifying an additional string to include
#         in the directory name.
#
# Usage notes:
#
# 1.  The returned temporary directory name is a string having the following
#     form:
#
#     $TMPDIR/$USER/$$ or $TMPDIR/$USER/<dir>/$$
#
#     where $USER is the current user's username and $$ is the process 
#     number.  The second form is used if an argument is specified.
#
# 2.  If the -c option is specified, all old files in $TMPDIR/$USER are
#     deleted (find $TMPDIR/$USER -mtime +1) whether or not they are related
#     to the current job.
#
# 3.  The top temporary directory ($TMPDIR) may be specified as an environment
#     variable.  If it is not specified, it defaults to /var/tmp or /usr/tmp.
#
# 4.  The returned directory name is guaranteed to be empty.
#
# Created 30-Dec-1993  Herbert Greenlee
#
##############################################################################
#
# Scan options
#
set clean = 0
set argv = (`getopt c $argv`)
while("$argv[1]" != '--')
  switch($argv[1])
  case -c:
    set clean = 1
    shift
    breaksw
  endsw
end
shift    # drop --
#
# Get optional direcotry name.
#
if( $#argv > 0 )then
  set dir = $argv[1]
else
  set dir = ''
endif
#
# Define temp file name
#
set tmpdir = ''
if( $?TMPDIR )then
  set tmpdir = $TMPDIR/$USER
endif
if ( "$tmpdir" == '' && -w /var/tmp )set tmpdir = /var/tmp/$USER
if ( "$tmpdir" == '' )set tmpdir = /usr/tmp/$USER
#
# Clean up?
#
if( $clean )then
  find $tmpdir -type f -mtime +1 -exec rm {} \; >& /dev/null
  find $tmpdir -depth -type d -mtime +1 -exec rmdir {} \; >& /dev/null
endif
#
# Add optional name and process number for uniqueness.
#
if( "$dir" != '' )set tmpdir = $tmpdir/$dir
set tmpdir = $tmpdir/$$
#
# Make sure that directory exists and is empty.
#
if ( -d $tmpdir )rm -rf $tmpdir
mkdirp $tmpdir
#
# Write name of directory on standard output
#
echo $tmpdir
