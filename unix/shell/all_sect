#! /bin/csh -f
#------------------------------------------------------------------------------
#
# Name      : all_sect
#
# Purpuse   : Output a list of all library sections in d0library
#
# Arguments : none
#
# Created 21-JUN-1991   Herbert B. Greenlee
#
#------------------------------------------------------------------------------
set nonomatch = 1
foreach dir ($d0library/*)
  if(-d $dir && -r $dir )then
    set sect = $dir:t
    set test_dir = $d0test/$sect
    set version_file = ( $dir/000_${sect}* )
    if( ! -f "$version_file[1]" && -d $test_dir ) \
      set version_file = ( $test_dir/000_${sect}* )
    if( -f "$version_file[1]" )echo $sect
  endif
end
