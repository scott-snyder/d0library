      SUBROUTINE MUMUOT(NTOT,IQUAD,F1,F2,F3,XI,YI,ZI,XX,YY,ZZ,
     & DCXI,DCYI,DCZI,DCXO,DCYO,DCZO,CHIB,CHINB,P,DP,ELCAL,ELFE,
     A BDL,CT,JADD,IPOINT,IDSOL,IDELT,IPAD)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CC
CC    CREATES and fills muon track banks MUOT and MHTT
CC
CC    INPUT: IQUAD  - QUADRANT OF TRACK
CC           F1,F2,F3  - FLAGGING WORDS
CC           NTOT   - TOTAL NUMBER OF HITS
CC           JADD   - WIRE ADDRESSES OF HITS
CC           IPOINT - LOCATION IN HIT BANK (MUOH) OF HITS
CC           IDSOL  - TIME NUMBER AND DRIFT SOLUTION OF HIT
CC           IDELT  - TIME NUMBER OF DELTA TIME SOLUTION
CC           IPAD   - PAD SOLUTION OF HIT
CC
CC    OUTPUT IN ZEBRA BANKS MUOT AND MUTH
CC
CC     HEDIN 9/88
CC     DH 11/88 ADD FLAGGING WORDS
CC     DH 3/89 ADD P,DP
CC     DH 9/89 ADD XI,YI,ZI
CC     DH 3/90 FLAG MUOH BANK THAT HITS ON THIS TRACK
CC     DH 4/90 ADD IXMAIN 
CC     DH 9/90 call bkmuot
CC     DH 1/91 add SAMUS, add energy loss
CC     DH 4/91  change NMAX from 20 to 40
CC     DH 10/91 ADD F3
CC     DH 2/92 28 words/MUOH hit
CC     DH 5/92 ADD BDL,CT
CC     DH 1/93 flag hit/track with - if not in fit or adjacent to
CC     a hit used in the fit
CC     DH 8/94 fix a-layer stub screwup
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT NONE
      INCLUDE 'D0$INC:ZEBCOM.INC'
      INTEGER NMAX,IQUAD,NTOT,I,L,LMTRH,GZMTRH,LMUOT,LMHTT
      INTEGER NMOD,NPLN,NWIR,JERR,NM,NP,NW,J
      PARAMETER (NMAX=40)
      INTEGER JADD(NMAX),IPOINT(NMAX),IDSOL(NMAX),
     A IPAD(NMAX),IDELT(NMAX),F1,F2,F3,LMUOH,GZMUOH,NTT,GZMUOT
      REAL XI,YI,ZI,ELCAL,ELFE,BDL,CT
      REAL CHIB,DCXI,DCYI,DCZI,DCXO,DCYO,DCZO,XX,YY,ZZ,CHINB,P,DP
C
CC   FILL UP OUTPUT ZEBRA BANK
CC
      LMTRH=GZMTRH(0)
      IQ(LMTRH+1)=IQ(LMTRH+1)+1   ! INDEX NUMBER OF TRACKS
      NTT=IQ(LMTRH+1)
C
      CALL BKMUOT(0,0,LMUOT)
      CALL BKMHTT(LMUOT,NTOT*5,LMHTT)
CC
      LMUOT=GZMUOT(NTT)
      IQ(LMUOT+1)=NTOT
      IQ(LMUOT+2)=0   
      IQ(LMUOT+3)=IQUAD
      IQ(LMUOT+4)=F1
      IQ(LMUOT+5)=F2
      IQ(LMUOT+6)=F3
      IQ(LMUOT+7)=0
      Q(LMUOT+ 8)=XI
      Q(LMUOT+ 9)=YI
      Q(LMUOT+10)=ZI
      Q(LMUOT+11)=XX
      Q(LMUOT+12)=YY
      Q(LMUOT+13)=ZZ
      Q(LMUOT+14)=DCXI
      Q(LMUOT+15)=DCYI
      Q(LMUOT+16)=DCZI
      Q(LMUOT+17)=DCXO
      Q(LMUOT+18)=DCYO
      Q(LMUOT+19)=DCZO
      Q(LMUOT+20)=CHIB
      Q(LMUOT+21)=CHINB
      Q(LMUOT+22)=BDL
      Q(LMUOT+23)=P
      Q(LMUOT+24)=DP
      Q(LMUOT+25)=ELCAL
      Q(LMUOT+26)=ELFE
      Q(LMUOT+27)=CT
CC
      LMUOH=GZMUOH(0)
      DO 10 I=1,NTOT
        IF(IAND(F2,4096).EQ.0) THEN    ! not a-layer duplicate
          IQ(LMUOH+28*(IPOINT(I)-1)+3)=NTT
CC  MARK DIFFERENTLY IF NOT ADJACENT TO HIT ON FIT
        IF(IDSOL(I).EQ.0.AND.CHIB.LT.900.) THEN
          CALL MUADD(JADD(I),NM,NP,NW,JERR)
          IF(NM.GE.100.AND.NM.LE.107) GO TO 11
          IF(NM.GE.140.AND.NM.LE.147) GO TO 11
          IF(NM.GE.200.AND.NM.LE.207) GO TO 11
          IF(NM.GE.240.AND.NM.LE.247) GO TO 11
          DO J=1,NTOT
            IF(IDSOL(J).NE.0) THEN
              CALL MUADD(JADD(J),NMOD,NPLN,NWIR,JERR)
              IF(NM.EQ.NMOD.AND.IABS(NWIR-NW).LE.1) GO TO 11
            ENDIF
          ENDDO
          IQ(LMUOH+28*(IPOINT(I)-1)+3)=-NTT
        ENDIF
11      CONTINUE
        ENDIF
        L=LMHTT+5*(I-1)
        IQ(L+1)=JADD(I)
        IQ(L+2)=IPOINT(I)
        IQ(L+3)=IDSOL(I)
        IQ(L+4)=IDELT(I)
        IQ(L+5)=IPAD(I)
   10 CONTINUE
      RETURN
      END
