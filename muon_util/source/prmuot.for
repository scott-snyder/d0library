      SUBROUTINE PRMUOT(PRUNIT,LMUT,NMUT,CFL,IFL)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CC
CC    PRINT OUT MUOT - MUON TRACK BANK
CC    ALSO WILL PRINT OUT MHTT - HITS ON MUON TRACKS
CC
CC    PRUNIT - UNIT NUMBER FOR PRINTOUT
CC    LMUT - BANK ADDRESS
CC    NMUT - BANK NUMBER
CC    CFL - FLAG TO CONTROL PRINT OUT 
CC    'ALL' OR 'LINEAR' GIVE COMPLETE STRUCTURE
CC    'ONE' GIVE FOR SINGLE TRACK LISAQ OR NISAQ MUST BE PROVIDED
CC    IFL - HOW MUCH TO PRINT 0 IS COMPLETE
CC
CC
CC    DH MARCH 1986 11-86 DH, NEW ZEBRA FORMAT
CC    DH 8/88 CHANGE MHTT FLAGS
CC    DH 10/88 CHANGE ADDRESS FORMAT
CC    DH 9/89 ADD XI,YI,ZI
CC    DH 2/90 CHANGE FORMAT
CC    DH 5/90 CHANGE DEFINITION OF TRACK NUMBER
CC    DH 1/91 ADD SAMUS
C-   Updated  12-JUN-1991   Daria Zieminska: print #points in wamus,samus 
CC    DH 3/92 IFW2, BDL
CC    DH 5/92 IFW2, CT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT NONE
      INCLUDE 'D0$INC:ZEBCOM.INC'
      INCLUDE 'D0$LINKS:IZMHTT.LINK/LIST'  
      INTEGER PRUNIT,LMUOT,GZMUOT,NMUOT,IFL,LTRACK,LHIT,LMHTT
      CHARACTER CFL*(*)
      INTEGER LZLOC,NMOD,I,J,K,LL,L,N,NPOINT,KK,LMUT,NMUT 
      INTEGER IADD,IMOD(12),IPLN(12),ICELL(12),IERR
CCCCCCCCCkACCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      LMUOT=LMUT
      NMUOT=NMUT
C
C     PRINT ONE BANK
C
      IF(CFL.EQ.'ONE') THEN
        IF(LMUOT.EQ.0) THEN
          IF(NMUOT.EQ.0) GO TO 98    ! ERROR EXIT
          LMUOT=LZLOC(IXMAIN,'MUOT',NMUOT)
        ENDIF
      ENDIF
C
C     PRINT ALL BANKS
C
      IF(CFL.EQ.'ALL'.OR.CFL.EQ.'LINEAR') THEN
        IF(LMUOT.EQ.0) THEN
          LMUOT=GZMUOT(0)
            IF(LMUOT.EQ.0) GO TO 98    ! ERROR EXIT
        ENDIF
      ENDIF
      WRITE(PRUNIT,101) IQ(LMUOT-5)
101   FORMAT('0',10X,' BANK MUOT: MUON TRACK BANK ',I4//)
C
      IF(IFL.GT.0) THEN          ! SHORT PRINTOUT
        WRITE(PRUNIT,102)
102   FORMAT(' NO. POINTS IN WAMUS SAMUS     X      Y       Z  ',
     A '  CHISQ (BEND + NON)   P ')
        LL=LMUOT
 10     CONTINUE 
        WRITE(PRUNIT,103) IQ(LL+1),IQ(LL+2),Q(LL+11),Q(LL+12),
     A  Q(LL+13),Q(LL+20),Q(LL+21),Q(LL+23)
103   FORMAT(12X,2I6,2X,4F8.1,2F8.2,F8.1)
        LL=LQ(LL)
        IF(LL.NE.0.AND.CFL.NE.'ONE') GO TO 10
      ENDIF
C
C    PRINT OUT COMPLETE BANK AND HITS ON TRACK
C     
      IF(IFL.EQ.0) THEN
        WRITE(PRUNIT,104)
104     FORMAT(' HIT(W+S) QUAD FLG1  FLG2   FLG3  FLG4    XI     YI',
     A         '    ZI    XMAG   YMAG   ZMAG '/)
        LL=LMUOT
 11     CONTINUE
        WRITE(PRUNIT,105)(IQ(LL+I),I=1,7),(Q(LL+I),I=8,13)
 105    FORMAT(1X,2I3,1X,I4,1X,I4,1X,Z6,1X,Z6,1X,I4,1X,6F7.1)
        LL=LQ(LL)
        IF(LL.NE.0.AND.CFL.NE.'ONE') GO TO 11
        WRITE(PRUNIT,106)
 106    FORMAT('0 XCSIN YCSIN ZCSIN XCSOT YCSOT ZCSOT',
     A  ' CHI(BD+NON) BDL  P   DELP ELCAL ELFE CT'/)
        LL=LMUOT
 12     CONTINUE
        WRITE(PRUNIT,107)(Q(LL+I),I=14,27)
107     FORMAT(6F6.2,F6.2,F6.0,F5.1,F7.0,F6.1,2F4.1,F5.0)
        LL=LQ(LL)
        IF(LL.NE.0.AND.CFL.NE.'ONE') GO TO 12
C
C    PRINT OUT HITS ON TRACKS
C
        WRITE(PRUNIT,108)
108     FORMAT('0  BANK MHTT: HITS ON MUON TRACKS  '/)
        LL=LMUOT
        KK=0
 13     CONTINUE
        NPOINT=IQ(LL+1)
        KK=KK+1
        LMHTT=LQ(LL-IZMHTT)
        WRITE(PRUNIT,109)KK
109     FORMAT('         MUON TRACK ',I3/)
        J=0
  20    CONTINUE
        K=8
        L=LMHTT+5*J
        IF(J+K.GT.NPOINT) K=NPOINT-J
        J=J+K
        DO 30 N = 1,K
        IADD = IQ(L+5*(N-1)+1)
        CALL MUADD(IADD,IMOD(N),IPLN(N),ICELL(N),IERR)
 30     CONTINUE
        WRITE(PRUNIT,111)(IMOD(N),IPLN(N),ICELL(N),N=1,K)
        WRITE(PRUNIT,112)(IQ(L+5*(N-1)+2),N=1,K)
        WRITE(PRUNIT,113)(IQ(L+5*(N-1)+3),N=1,K)
        WRITE(PRUNIT,114)(IQ(L+5*(N-1)+4),N=1,K)
        WRITE(PRUNIT,115)(IQ(L+5*(N-1)+5),N=1,K)
        WRITE(PRUNIT,140)
        IF(J.LT.NPOINT) GO TO 20
        LL=LQ(LL)
        IF(LL.NE.0.AND.CFL.NE.'ONE') GO TO 13
      ENDIF
 111  FORMAT(' WIREADD    ',8(I3,I2,I3,1X))
 112  FORMAT(' MUOH HIT   ',8(I4,5X))
 113  FORMAT(' TIME SIGN  ',8(I4,5X))
 114  FORMAT(' DELTA TIME ',8(I4,5X))
 115  FORMAT(' PAD        ',8(I4,5X))
 140  FORMAT('0')
      RETURN
 98   CONTINUE
      WRITE(PRUNIT,99)
 99   FORMAT(' PRMUOT:NO MUOT BANK ')
      RETURN
      END
