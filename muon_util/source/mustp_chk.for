      INTEGER FUNCTION MUSTP_CHK()
C----------------------------------------------------------------------
C-
C-   Purpose and Methods : Check matching between STP and GEANT for MUON
C-                         Print status and recomendation. 
C-   Inputs  : None 
C-   Outputs : None
C-   Return  : 0 - match or real data,  no problem
C-             1 - mismacth
C-             2 - event has no MUD1 bank or data bank
C-             3 - No STP
C-
C-   Created  15-APR-1992   Atsushi Taketani
C-            03-MAY-!992   Put in ERRMSG to replace WRITE(6,*) HTD
C- 
C-   Need to modify for muon scintillator software installtion
C----------------------------------------------------------------------
      IMPLICIT NONE
      INCLUDE      'D0$INC:ZEBSTP.INC'
      INCLUDE      'D0$INC:ZEBCOM.INC'
      INTEGER      LMUD1, GZMUD1
      INTEGER      VDMGEH, VDMMAH, VSMGEH, VSMMAH
      INTEGER      GZMGEH, LMMAH, GZMMAH 
      CHARACTER*60 ERROR_MESSAGE
C----------------------------------------------------------------------
C
      MUSTP_CHK = 0
C
      IF ( LHEAD.EQ.0 ) THEN            ! data bank?
        ERROR_MESSAGE = 'No Data Bank Exists? MUSTP_CHK=2'
        CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        MUSTP_CHK = 2
        GOTO 999
      END IF
C
      IF ( IQ(LHEAD+1).LE.999 ) THEN    ! for real data
        ERROR_MESSAGE = 'Analyzing Real Data'
        CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        MUSTP_CHK = 0
        GOTO 999
      END IF
C
      LMUD1 = GZMUD1()                  ! MUD1 exist?
      IF ( LMUD1.EQ.0 ) THEN
        ERROR_MESSAGE = 'No MUD1 Data Bank Exists? MUSTP_CHK=2'
        CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        MUSTP_CHK = 2
        GOTO 999
      END IF
C
      VDMGEH = IQ(LMUD1+5)              ! MGEH version for GEANT
      VDMMAH = IAND(IQ(LMUD1+6),65535)  ! MMAH vesrion for GEANT
C
      LMGEH = GZMGEH(0)                 ! STP exist?
      LMMAH = GZMMAH(0)
      IF ( LMGEH.EQ.0 ) THEN
        ERROR_MESSAGE = 'No MGEH Bank. MUSTP_CHK=3'
        CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        MUSTP_CHK = 3 
      END IF
      IF ( LMMAH.EQ.0 ) THEN
        ERROR_MESSAGE = 'No MMAH Bank. MUSTP_CHK=3'
        CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        MUSTP_CHK = 3 
      END IF
      IF ( MUSTP_CHK.NE.0 ) THEN
        GOTO 999
      END IF
C
      VSMGEH = IC(LMGEH+1)             ! MGEH version at STP
      VSMMAH = IC(LMMAH+1)             ! MMAH vesrion at STP
C
      IF ( VSMGEH.NE.VDMGEH ) THEN
        MUSTP_CHK = 1
        ERROR_MESSAGE = 'Muon Geometry Mismatch'
        CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        WRITE(ERROR_MESSAGE,101) VDMGEH
        CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        WRITE(ERROR_MESSAGE,102) VSMGEH
        CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')

        IF ( VDMGEH .EQ. 0 ) THEN
          ERROR_MESSAGE = 'Use d0$stp:muo_stpfile.dat'
          CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        ELSE IF ( VDMGEH.EQ.2 ) THEN
          ERROR_MESSAGE = 'Use d0$stp:muo_stpfile_1.dat'
          CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        ELSE
          ERROR_MESSAGE = 'We dont have that STP in the official area'
          CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        END IF
      ELSE
        ERROR_MESSAGE = 'MUON GEOMETRY MATCH'
        CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
      END IF
C
      IF ( VSMMAH.NE.VDMMAH ) THEN
        MUSTP_CHK = 1
          ERROR_MESSAGE = ' Muon magnetic field mismatch'
          CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
         WRITE(ERROR_MESSAGE,103) VDMMAH 
         CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
         WRITE(ERROR_MESSAGE,104) VSMMAH 
         CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        IF ( VDMMAH .EQ. 0 ) THEN
          ERROR_MESSAGE = 'Use d0$stp:muo_stpfile.dat'
          CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        ELSE IF ( VDMMAH.EQ.2 ) THEN
          ERROR_MESSAGE = 'Use d0$stp:muo_stpfile_1.dat'
          CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        ELSE
          ERROR_MESSAGE = ' No STP like that in official area.'
          CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
        END IF
      ELSE
          ERROR_MESSAGE = ' Muon magnetic field MATCH'
          CALL ERRMSG('Muon','MUSTP_CHK',ERROR_MESSAGE,'W')
      END IF
C
 101    FORMAT(1X,'MC DATA Generated By Version ',I4)
 102    FORMAT(1X,'MC DATA Generated Using Stp Version ',I4)
 103    FORMAT(1X,'MC MAGNET Generated By Version ',I4)
 104    FORMAT(1X,'MC MAGNET Generated Using Stp Version ',I4)

  999 RETURN
      END
