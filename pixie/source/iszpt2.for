C===========================================================
      SUBROUTINE ISZPT2(NPART,ID,PX,PY,PZ,P,THETA)
C============================================================
C
C  Description:  Displays tracks generated by ISAJET with
C  ============  with lengths proportional to Pt (2 D)
C
C  Author:
C  =======
C  Tami Kramer
C
C  Revision History:
C  ==================
C  Original Creation - May 14,1987
C  Update   Jan 4, 1990 Lupe Howell - Color table implemented
C
C=============================================================
C
      IMPLICIT NONE
      INCLUDE 'D0$INC:GRAPHF77.INC'
C
C  Local Declarations:
C  ====================
C
      INTEGER J               ! Loop variables
      INTEGER I3D             ! Calling mode indicator for ISZEVT
      INTEGER SI              ! Max no. of particles
      PARAMETER(SI=1000)
      INTEGER ID(SI)          ! ID of particles returned from GIPART
      INTEGER NPART           ! Actual no. of parts returned from GIPART
      LOGICAL LOGSCA          ! Flag to plot using log scale
      REAL PX(SI),PY(SI)      ! XYZ components of momentum
      REAL PZ(SI)
      REAL P(SI)              ! Total momentum
      REAL THETA(SI)          ! Angles of particle
      REAL THMDE              ! Min. theta in radians and degrees
      REAL PT(SI)             ! Calculated Pt of track
      REAL PTMAX              ! Max Pt of all particles - used to set
C                             ! windows for graphics.
      REAL RAD1,RAD2
      REAL RAD3               ! Radii of reference circles
      REAL PTSCAL             ! Scaling factors for displaying 
      LOGICAL LASER
C
C  Data Statments:
C  ===============
C
C
C  Executable Code:
C  ================
C
      PTMAX = 0.
      DO 36 J = 1, NPART
         PT(J) = SQRT(PX(J)**2+PY(J)**2)
         IF (PT(J) .GT. PTMAX) PTMAX = PT(J)
   36 CONTINUE
      CALL PUGETV('LOG SCALE ON',LOGSCA)
      IF (LOGSCA) PTMAX = LOG(PTMAX)-LOG(.1)
      PTSCAL = 1.1*PTMAX
      CALL JWCLIP(.TRUE.)
      CALL JWINDO(-PTSCAL,PTSCAL,-PTSCAL,PTSCAL)
      CALL PUOPEN
         CALL PXCOLR('GRE') 
C
C  Draw graphical circles depending on whether one is using a 
C  log scale or not.
C  ===========================================================
C
         RAD1 = 1.
         RAD2 = 10.
         RAD3 = 100.
         IF (LOGSCA) THEN
            RAD1 = LOG(RAD1)-LOG(.1)
            RAD2 = LOG(RAD2)-LOG(.1)
            RAD3 = LOG(RAD3)-LOG(.1)
         ENDIF
         CALL JCIRCL(0.,0.,0.,RAD1,2)
         IF (PTMAX .GE. RAD2) CALL JCIRCL(0.,0.,0.,RAD2,2)
         IF (PTMAX .GE. RAD3) CALL JCIRCL(0.,0.,0.,RAD3,2)
C
C  Display tracks...
C  ===================
C
         I3D = 0
         CALL ISZEVT(NPART,ID,PX,PY,PZ,PT,P,THETA,I3D)
      CALL JRCLOS
C
  999 CONTINUE
      RETURN
      END
C
