      SUBROUTINE PCTREV
C----------------------------------------------------------------------
C-
C-   Purpose and Methods : Calorimeter End View Energy Histogram for
C-                         Trigger System
C-
C-   Inputs  :
C-   Outputs :
C-   Controls:
C-
C-   Created   3-APR-1991   Nobuaki Oshima
C-
C----------------------------------------------------------------------
      IMPLICIT NONE
      INCLUDE 'D0$INC:ZEBCOM.INC/LIST'
      INCLUDE 'D0$LINKS:IZTRGR.LINK'
      INCLUDE 'D0$INC:PI.DEF'
      INCLUDE 'D0$PARAMS:L1PHP.PARAMS'
      INCLUDE 'D0$PARAMS:CAL_OFFLINE.PARAMS'
      REAL ETEM(NPHIL1,-NETAL1:NETAL1)
      REAL ETHD(NPHIL1,-NETAL1:NETAL1)
      REAL EHPH(NPHIL1),EMPH(NPHIL1),EMAX,X(4),Y(4),SCL
      REAL CENPHI,DELPHI,PHI1,PHI2,EMSCL,EHSCL
      REAL PCMIN,PCMAX
      REAL ETTOT,ETELM,ETHAD
      REAL CENRAD,DELRAD,CENZED,DELZED
      INTEGER RUN,ID,RUNSAVE,IDSAVE, TYP,IER
      INTEGER IETAMN,IETAMX,IPHI,IDPHI
      INTEGER IETAMN2,IETAMX2
      INTEGER IHIND,L1IPHI,L1IETA,JPHI,IETA
      INTEGER IPHIL1,IDPHIL1
      INTEGER ARGSOK,NUM,NVAL
      SAVE RUNSAVE,IDSAVE
      LOGICAL EZERROR,PICPHI
      CHARACTER*24 STR1,STR2
      CHARACTER*3  COLORS(6)
      CHARACTER*15 LABELS(6)
      CHARACTER*4  CVAL
C-
      DATA COLORS /6*'   '/
      DATA LABELS /6*'   '/
C----------------------------------------------------------------------
C-
      CALL EZPICK('PX_CALDIS_RCP')          ! Selecting Caldis bank
      IF ( EZERROR(IER) ) THEN
        CALL ERRMSG('PIXIE','PCTREV','Bank PX_CALDIS_RCP NOT FOUND','W')
        GOTO 999
      ENDIF
C-
C--- Get parameters
C-
      CALL PUGETV('CAL IETAMIN', IETAMN)
      IF(IETAMN.LT.0) THEN
        IETAMN2=(IETAMN-1)/2
      ELSE
        IETAMN2=(IETAMN+1)/2
      ENDIF
      CALL PUGETV('CAL IETAMAX', IETAMX)
      IF(IETAMX.LT.0) THEN
        IETAMX2=(IETAMX-1)/2
      ELSE
        IETAMX2=(IETAMX+1)/2
      ENDIF
      CALL PUGETV('CAL PHI', IPHI)
      CALL PUGETV('CAL DPHI', IDPHI)
      CALL PUGETV('CAL PICK PHI', PICPHI)
      IF (IETAMN2 .GT. IETAMX2)   IETAMN2 = IETAMX2
C-
C--- Impose up and down region in the CAL SIDE view
C-
      IF(IPHI .GT. NPHIL/2) IPHI = IPHI - NPHIL/2
C-
C--- GET TRGR BANK
C-
      CALL VZERO(ETEM,NPHIL1*(2*NETAL1+1))
      CALL VZERO(ETHD,NPHIL1*(2*NETAL1+1))
C--- L1IPHI & L1IETA are same order in EM(0-1279) & HAD(1280-2559)
      DO IHIND=0,1279
        CALL CHOTTT(IHIND,L1IPHI,L1IETA)
        IF(L1IETA.GE.-20 .AND. L1IETA.LE.20) THEN
          CALL PCTRGR(L1IPHI,L1IETA,1,ETELM)
          CALL PCTRGR(L1IPHI,L1IETA,0,ETTOT)
          IF(ETELM .GT. 0.) ETEM(L1IPHI,L1IETA) = ETELM
          IF (ETTOT .GT. ETELM) THEN
            ETHAD = ETTOT - ETEM(L1IPHI,L1IETA)
            ETHD(L1IPHI,L1IETA) = ETHAD
          ENDIF
        ENDIF
      ENDDO
C--- Get CC inside & outside radius
      CALL EVNTID(RUN,ID)
      IF(RUN.NE.RUNSAVE.OR.ID.NE.IDSAVE) THEN
        RUNSAVE = RUN
        IDSAVE  = ID
        CALL CALRAD(1,MNLYEM,CENRAD,DELRAD,CENZED,DELZED,ARGSOK)
        PCMIN = CENRAD - DELRAD/2.
        CALL CALRAD(1,MNLYCH,CENRAD,DELRAD,CENZED,DELZED,ARGSOK)
        PCMAX = CENRAD + DELRAD/2.
      ENDIF
C-
      EMAX=0.
      DO JPHI=1,NPHIL1
        EMPH(JPHI)=0.
        EHPH(JPHI)=0.
        DO 30 IETA=IETAMN2,IETAMX2
          IF(IETA.EQ.0) GO TO 30
          EMPH(JPHI)=EMPH(JPHI)+ETEM(JPHI,IETA)
          EHPH(JPHI)=EHPH(JPHI)+ETHD(JPHI,IETA)
   30   CONTINUE
        IF(EMPH(JPHI)+EHPH(JPHI) .GT. EMAX) THEN
          EMAX=EMPH(JPHI)+EHPH(JPHI)
        ENDIF
      ENDDO
      IF(EMAX.LE.0.) GO TO 150
C
C--- CALCULATE THE PHI LIMITS CURRENTLY IN EFFECT
C--- ( NOTE THAT JARC REQUIRES DEGREES )
      CALL CALPHI(IPHI-IDPHI,1,CENPHI,DELPHI,ARGSOK)
      PHI1=(CENPHI-DELPHI/2.)/RADIAN
      CALL CALPHI(IPHI+IDPHI,1,CENPHI,DELPHI,ARGSOK)
      PHI2=(CENPHI+DELPHI/2.)/RADIAN
C- CALCULATE THE ENERGY SCALE SO EMAX FILLS THE CELL
      SCL=(PCMAX-PCMIN)/EMAX
      CALL PUOPEN
C- DRAW THE CURRENT PHI LIMITS
      CALL JARC(0.,0.,0.,PCMAX+5.,0,PHI1,PHI2)
C- DRAW EACH CELL WITH HISTOGRAM OF ITS ENERGY
      NUM = 1
      JPHI = 0
      DO 100 IPHI=1,NPHIL,2
        CALL CALPHI(IPHI,1,CENPHI,DELPHI,ARGSOK)
        PHI1=CENPHI-DELPHI
        PHI2=CENPHI+DELPHI
        JPHI = JPHI + 1
        EMSCL=SCL*EMPH(JPHI)
        EHSCL=SCL*EHPH(JPHI)
        CALL PCEVCL(PCMIN,PCMAX,PHI1,PHI2,2,EMSCL,EHSCL,0.,
     &             COLORS,LABELS,NUM)
  100 CONTINUE
      CALL JRCLOS
      CALL LEGEND(COLORS,LABELS,(NUM-1) )
  150 WRITE(STR1,200) EMAX
      WRITE(STR2,210) IETAMN2,IETAMX2
      CALL PUMESS(STR1)
      CALL PUMESS(STR2)
C-
  900 CONTINUE
C-
C--- PICK IPHI
C-
C.      IF (PICPHI) THEN
C.        CALL PUPHI
C.      ENDIF
C-
C--- Reset RCP bank
C-
      CALL EZRSET
  999 RETURN
C-
C--- FORMATS
C-
  200 FORMAT('  CAL ETMAX= ',F6.1,' GeV')
  210 FORMAT('  ETA(MIN:',I3,'-MAX:',I3,')')
      END
