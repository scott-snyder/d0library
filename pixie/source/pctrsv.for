      SUBROUTINE PCTRSV
C----------------------------------------------------------------------
C-
C-   Purpose and Methods : HISTOGRAM Calorimeter Energies (Side View)
C-                         for Trigger System
C-   Inputs  :
C-   Outputs :
C-
C-   Created   3-APR-1991   Nobuaki Oshima
C-
C----------------------------------------------------------------------
      IMPLICIT NONE
C----------------------------------------------------------------------
C
      INCLUDE 'D0$INC:ZEBCOM.INC'
      INCLUDE 'D0$LINKS:IZTRGR.LINK'
      INCLUDE 'D0$PARAMS:L1PHP.PARAMS'
      INCLUDE 'D0$PARAMS:CAL_OFFLINE.PARAMS'
      REAL ETEM(NPHIL1,-NETAL1:NETAL1)
      REAL ETHD(NPHIL1,-NETAL1:NETAL1)
      REAL EMPHI(2,-NETAL1:NETAL1),EHPHI(2,-NETAL1:NETAL1)
      REAL EMAX,SCL,TH1,TH2,EMSCL,EHSCL,CENTH,DELTH
      REAL ENER,ETELM,ETHAD,ETTOT
      REAL DIST,X1,Y1,X2,Y2, X,Y,Z
      REAL CENRAD,DELRAD,CENZED,DELZED
      REAL PCR1,PCR2,PCZ1,PCZ2, TTH
      INTEGER JPHI,MNPHI,MXPHI,IETA,IPHI,IETAS,ARGSOK
      INTEGER IETA2,IHIND,L1IPHI,L1IETA, NUM
      INTEGER RUN,ID,RUNSAVE,IDSAVE
      LOGICAL EZERROR
      SAVE RUNSAVE,IDSAVE
      INTEGER NETACC, TYP,I,IER
      CHARACTER*20 STR1
      CHARACTER*3 COLORS(6)
      CHARACTER*15 LABELS(6)
C
      DATA COLORS /6*'   '/
      DATA LABELS /6*'   '/
      DATA NETACC /12/
C----------------------------------------------------------------------
C-
      CALL EZPICK('PX_CALDIS_RCP')          ! Selecting Caldis bank
      IF ( EZERROR(IER) ) THEN
        CALL ERRMSG('PIXIE','PCTRSV',
     &    'Bank PX_CALDIS_RCP NOT FOUND','W')
        GOTO 999
      ENDIF
C-
C--- DETERMINE CURRENT PHI
      CALL PUGETV('CAL PHI',IPHI)
      CALL PUGETV('CAL DPHI',JPHI)
      CALL PUGETV('CAL ETA',IETAS)
C-
C--- Impose up and down region in the CAL SIDE view
C-
      IF(IPHI .GT. NPHIL/2) IPHI = IPHI - NPHIL/2
C-
C--- GET CAEP BANK (ONCE/EVENT)
      CALL VZERO(ETEM,NPHIL1*(2*NETAL1+1))
      CALL VZERO(ETHD,NPHIL1*(2*NETAL1+1))
C--- L1IPHI & L1IETA are same order in EM(0-1279) & HAD(1280-2559)
      DO IHIND=0,1279
        CALL CHOTTT(IHIND,L1IPHI,L1IETA)
        IF(L1IETA.GE.-20 .AND. L1IETA.LE.20) THEN
          CALL PCTRGR(L1IPHI,L1IETA,1,ETELM)
          CALL PCTRGR(L1IPHI,L1IETA,0,ETTOT)
          IF(ETELM .GT. 0.) ETEM(L1IPHI,L1IETA) = ETELM
          IF (ETTOT .GT. ETELM) THEN
            ETHAD = ETTOT - ETEM(L1IPHI,L1IETA)
            ETHD(L1IPHI,L1IETA) = ETHAD
          ENDIF
        ENDIF
      ENDDO
C-
C--- SETUP PC_PARAMETERS
      CALL EVNTID(RUN,ID)
      IF(RUN.NE.RUNSAVE.OR.ID.NE.IDSAVE) THEN
        RUNSAVE = RUN
        IDSAVE  = ID
        TTH = TAN(2.*ATAN(EXP(-NETACC/10.)))
        CALL CELXYZ(26, 1, MXLYEM, X, Y, Z, ARGSOK)
        PCR1 = Z*TTH
        PCZ1 = Z
        CALL CALRAD( 1,MNLYCH,CENRAD,DELRAD,CENZED,DELZED,ARGSOK)
        PCR2 = CENRAD + DELRAD/2.
        PCZ2 = PCR2/TTH
        PCZ1 = PCZ1 - 9.
        PCZ2 = PCZ2 - 12.
      ENDIF
C-
      MNPHI=IPHI-JPHI
      IF(MNPHI.LT.1) MNPHI=MNPHI+NPHIL
      MXPHI=IPHI+JPHI
      IF (JPHI .EQ. 15) MXPHI = MXPHI + 1 !    Nobu. (23-JAN-1991)
      IF(MXPHI.LT.MNPHI) MXPHI=MXPHI+NPHIL
      CALL VZERO(EMPHI,2*(2*NETAL1+1))
      CALL VZERO(EHPHI,2*(2*NETAL1+1))
C--- SUM ENERGY OVER CURRENT PHI LIMITS
C--- UPPER PHI SLICE ON PLOT WILL BE THE CURRENT PHI
      DO 50 IETA=-NETAL1,NETAL1
        IF(IETA.EQ.0) GO TO 50
        DO 40 IPHI=1,2
          DO 30 I=MNPHI,MXPHI,2
            IF(IPHI.EQ.1) THEN
              JPHI = MOD(I-1,NPHIL)+1
              JPHI = (JPHI+1)/2
            ELSE
              JPHI=MOD(NPHIL/2+I-1,NPHIL)+1
              JPHI = (JPHI+1)/2
            ENDIF
            EMPHI(IPHI,IETA)=EMPHI(IPHI,IETA)+ETEM(JPHI,IETA)
            EHPHI(IPHI,IETA)=EHPHI(IPHI,IETA)+ETHD(JPHI,IETA)
   30     CONTINUE
   40   CONTINUE
   50 CONTINUE
      EMAX=0.
C--- FIND MAXIMUM ENERGY IN A CELL
      DO 100 IETA=-NETAL1,NETAL1
        IF(IETA.EQ.0) GO TO 100
        DO 110 IPHI=1,2
          EMAX=MAX(EMAX,EMPHI(IPHI,IETA)+EHPHI(IPHI,IETA))
  110 CONTINUE
  100 CONTINUE
      IF(EMAX.LE.0.) GO TO 1100
C--- CALCULATE SCALE SO MAX ENERGY FILLS A CELL
      SCL=MIN((PCR2-PCR1)/EMAX,(PCZ2-PCZ1)/EMAX)
C--- CALCULATE LINE TO SHOW CURRENT ETA
      IF(IETAS.NE.0) THEN
        CALL CALTH(IETA,CENTH,DELTH,ARGSOK)
        IF(ARGSOK.NE.0) GO TO 500
        IF(ABS(IETA).LE.NETACC) THEN
C--- CURRENT ETA IS IN CC
          DIST=PCR2+5.
          X1=DIST/TAN(CENTH+DELTH)
          Y1=DIST
          X2=DIST/TAN(CENTH-DELTH)
          Y2=DIST
        ELSE
C--- CURRENT ETA IS IN EC
          DIST=PCZ2+5.
          X1=DIST
          Y1=DIST*TAN(CENTH+DELTH)
          X2=DIST
          Y2=DIST*TAN(CENTH-DELTH)
        ENDIF
      ENDIF
  500 CONTINUE
      CALL PUOPEN
C--- DRAW CURRENT ETA VALUE
      IF(IETAS.NE.0) THEN
        CALL JMOVE(X1,Y1)
        CALL JDRAW(X2,Y2)
      ENDIF
C-
C--- DRAW CELLS WITH HISTOGRAM OF ENERGY IN CELL
      NUM = 1
      DO 1000 IETA2=-NETAL1,NETAL1
        IF(IETA2.EQ.0) GO TO 1000
        IETA=2*IETA2
        CALL CALTH(IETA,CENTH,DELTH,ARGSOK)
        IF(ARGSOK.NE.0) GO TO 1000
        TH1=CENTH+DELTH
        TH2=CENTH-DELTH
        IF (IETA2 .LT. 0) THEN
          TH1 = TH1 - .047
          TH2 = TH2 - .047
        ELSE
          TH1 = TH1 + .047
          TH2 = TH2 + .047
        ENDIF
        IF(ABS(IETA).LE.NETACC) THEN
C--- DRAW CC CELLS IN UPPER AND LOWER PHI SLICES
          EMSCL=SCL*EMPHI(1,IETA2)
          EHSCL=SCL*EHPHI(1,IETA2)
          CALL PCSVCC(PCR1,PCR2,TH1,TH2,2,EMSCL,EHSCL,0.,COLORS,
     &                LABELS,NUM)
          EMSCL=SCL*EMPHI(2,IETA2)
          EHSCL=SCL*EHPHI(2,IETA2)
          CALL PCSVCC(PCR1,PCR2,-TH1,-TH2,2,EMSCL,EHSCL,0.,COLORS,
     &         LABELS,NUM)
        ELSE
C--- DRAW EC CELLS IN UPPER AND LOWER PHI SLICES
          EMSCL=SCL*EMPHI(1,IETA2)
          EHSCL=SCL*EHPHI(1,IETA2)
          CALL PCSVEC(PCZ1,PCZ2,TH1,TH2,2,EMSCL,EHSCL,0.,COLORS,
     &                LABELS, NUM)
          EMSCL=SCL*EMPHI(2,IETA2)
          EHSCL=SCL*EHPHI(2,IETA2)
          CALL PCSVEC(PCZ1,PCZ2,-TH1,-TH2,2,EMSCL,EHSCL,0.,COLORS,
     &                 LABELS,NUM)
        ENDIF
 1000 CONTINUE
      CALL JRCLOS
      CALL LEGEND(COLORS,LABELS,(NUM-1) )
 1100 WRITE(STR1,2000) EMAX
 2000 FORMAT(' Max ET = ',F6.1,' GeV')
      CALL PUMESS(STR1)
C-
C---  Reset RCP bank
      CALL EZRSET
  999 RETURN
      END
