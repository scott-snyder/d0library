      SUBROUTINE ISAMPUNCH(ID,E,ABS1,IPCH1,PROB1,PROBC,EOUT)
CC  SEES IF PUNCHTHROUGH
C   ID + E ARE INPUT PARTICLES ID AND ENERGY
CC  ABS1  ARE ABSORPTION LENGTHS IN CC AND CF
CC  PROB1  ARE PROBABILITIES OF PUNCHTHROUGH
CC  IPCH1  = 0 NO PUNCHTHROUGH
CC  PROBC = COULOMB CUT REDUCTION\
CC  EOUT IS MOMENTUM EXITING ABSORBER
CC     UPDATED 5-87 DH, DH 10/89  DH 11/90
CC   DH 4/91 IABS(ID)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT NONE
      INTEGER ID,IPCH1,IF
      REAL E,ABS1,PROB1,PROBC,EOUT,RLEFF,RLPRI,D(11),F,P,RANF
      DATA D/1.,.98,.02,.005,.001,.0003,.0001,.00003,.00001,.000003,
     A  .000001/
      IPCH1=0
      PROB1=0.
      PROBC=0.
      EOUT=0.
      IF(IABS(ID).GE.10.AND.IABS(ID).LE.16) RETURN          ! SKIP LEPTONS
      IF(IABS(ID).EQ.110) RETURN                      ! PI0
CC  USE WA1 PARAMETERIZATION
      RLEFF=.89*E**.165
      RLPRI=1.53*E**.33
      IF(ABS1.LT.RLPRI) THEN
        PROB1=1.
      ELSE
        PROB1=EXP(-(ABS1-RLPRI)/RLEFF)
      ENDIF
      IF(RANF().LE.PROB1) IPCH1=1
CCCC COULOMB CUT REDUCTION; TAKEN FROM D0 TEST BEAM SO ONLY REALY
CCCC APPLICABLE AT 14 LAMBDA. MEASURED VALUES ARE P=50,100,150
CCCC FACTOR = .05,.004,.0016. WILL USE A SIMPLE MINDED 1/P**2 UP
CCCC TO A MAX VALUE OF .005 (THIS IS ALWAYS LARGER THAN MEASURED)
C
      PROBC=125/E**2
      IF(PROBC.GT.1.)   PROBC=1.
      IF(PROBC.LE..005) PROBC=.005
C   SEE OUTPUT ENERGY USING SIMPLE FRAGMENTATION FUNCTION FROM DAN GREEN
1     F=RANF()
      IF=F/.1 + 1
      IF(IF.GT.10) IF=10
      P=D(IF) +(10.*F-(IF-1))*(D(IF+1)-D(IF))
      IF(RANF().GT.P) GO TO 1
      EOUT=E*F
      RETURN
      END
