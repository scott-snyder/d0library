
       SUBROUTINE ISAMPROJ(THET,PHI,XAIR,ABS1,ABS2,DE1,DE2)
CCCCCCCCCCCCC
CCC    SIMPLE GEOMETRY TRACING FOR THE D0 MUON SYSTEM
CCCC   DETERIMES XAIR = DISTANCE TO FIRST SOLID MATERIAL
CCCC   ABS1 AND ABS2  = ABSORPTION LENGTHS THROUGH CAL. AND IRON
CCCC   DE1 AND DE2    = ENERGY LOSS IN CAL. AND IRON
CC     INPUT THET,PHI --- DIRECTION OF INTEREST
CC     ORIGINAL 8/84 DH
CC     BUG IN PHI CENTRAL REGION  11/87 DH
CC     ABS1 & ABS2 UPDATED 3/91 DKF
CC     Updated 5-MAR-94  Roy Thatcher 
CC        Changed ENTRY to ENTRY1 to eliminate spurious lines in D0ENTRY -
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT NONE
      REAL THET,PHI,XAIR,ABS1,ABS2,DE1,DE2,DENUR,ABSUR,ABSFE,
     A DEDXUR,DEDXFE,CC,CF,EC,EF,RCYN,ZCYN,CTH,STH,XCEN,XEND,
     A THETD,CPHI,THETA
      REAL DATA(2,46)
      INTEGER IPHI,I,ENTRY1
      LOGICAL FIRST
      DATA FIRST/.TRUE./
      DATA DENUR/18.95/
      DATA ABSUR,ABSFE,DEDXUR,DEDXFE/199.,132.,1.1,1.48/
      DATA CC,CF,EC,EF/6.9,6.0,8.9,8.2/
      DATA RCYN,ZCYN/.7,1.6/
      DATA DATA/0.0,0.0,9.7,18.9,9.7,18.9,9.7,18.9,9.7,17.1,9.7,17.1,
     A9.7,17.0,9.0,18.0,8.0,17.2,8.6,17.9,8.9,18.5,9.3,19.0,9.8,19.7,
     A9.8,19.6,9.1,19.3,8.9,19.2,9.0,19.4,9.0,18.8,9.2,16.0,8.0,10.6,
     A7.5,9.6,7.3,9.8,7.1,11.6,7.0,14.5,6.9,15.3,6.9,15.0,6.4,14.3,6.0,
     A13.7,5.8,12.8,6.4,13.6,7.0,14.3,7.5,14.5,7.4,14.4,7.3,14.3,7.3,
     A14.1,7.2,14.0,7.2,13.9,7.1,13.8,7.1,13.6,7.0,13.5,7.0,13.4,7.0,
     A13.4,7.0,13.3,6.9,13.3,6.9,13.2,6.9,13.2/
CC
CC    USE ONE HALF
      CTH=ABS(COS(THET))
      IF(CTH.LT..00001) CTH=.00001
      STH=SQRT(1.-CTH**2)
      IF(STH.LT..00001) STH=.00001
CC  FIND DISTANCE TO CALORIMETER
      XCEN=RCYN/STH
      XEND=ZCYN/CTH
CC PICK SMALLER
      XAIR=XCEN
      IF(XEND.LT.XCEN) XAIR=XEND
CC ADD ONE ABSORPTION LENGTH IN UR
      XAIR=XAIR+ABSUR/DENUR*.01
CC  UPDATED ABS1 ABS2 - APPROX. 2 DEGREE RESOLUTION
      IPHI=2.*(PHI+3.14159/4.)/3.14159+1.
      IF(IPHI.EQ.1.OR.IPHI.EQ.3.OR.IPHI.EQ.5) THEN
        CPHI=ABS(COS(PHI))
      ELSE
        CPHI=ABS(SIN(PHI))
      ENDIF
      THETD = THET*180./3.141593
      IF (THETD.GT.90.) THETD = 180. - THETD
      IF (THETD.LE.90.0.AND.THETD.GE.0.0) THEN
        ENTRY1 = NINT((THETD/2.0) + 1.0)
        ABS1 = DATA(1,ENTRY1)
        ABS2 = DATA(2,ENTRY1)
        IF(THETD.GT.40.) THEN    ! CORRECT FOR PHI DEPENDENCE
          ABS2=ABS1+(ABS2-ABS1)/CPHI
        ENDIF
      ELSE
        ABS1 = 0.0
        ABS2 = 0.0
      END IF
C
CC  SCALE ENERGY LOSS FROM ABSORPTION LENGTHS
CC  ASSUME CAL ALL URANIUM
      DE1=ABS1*ABSUR*DEDXUR*.001
      DE2=DE1+(ABS2-ABS1)*ABSFE*DEDXFE*.001
      RETURN
      END
