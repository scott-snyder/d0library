      SUBROUTINE ISAMTREFF(THETA,PHI,P,ITRIG)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     SEE IF MUON PASSED TRIGGER
C     INPUT: THETA ,PHI, MUON MOMENTUM
C     OUTPUT: ITRIG=0, DIDN'T PASS, =1 PASSED CCT, =2 PASSED OTC
C                       = 3 PASSED BOTH
C     HEDIN 8-87, 11-90
C     SIMPLE MINDED, ALL PASS CCT, NO PHI DEPENDENCE
C------------------------------------------------------------------------
      IMPLICIT NONE
C
      REAL P,THETA,THET,PT,DEL,EFF,EFFP(18),EFFPT(11),PHI,RANF
      INTEGER IP,ITRIG
C
C   EFFICIENCY VS PT FOR CENTRAL MUONS: IN 1 GEV INTERVALS STARTING AT 0
      DATA EFFPT/0.,0.,0.,0.,.1,.4,.6,.84,.90,.96,1.0/
C
C   EFFICIENCY VS P FOR FORWARD MUONS: IN 1 GEV INTERVALS STARTING AT 0
      DATA EFFP/0.,0.,0.,0.,0.,0.,.05,.18,.30,.42,.55,.68,.80,.87,
     A .93,.95,.98,1./
      ITRIG=0
CCCC     DO CCT
      ITRIG=1            ! ALL PASS
CCCCCCCCCCC
      THET=180./3.14159*THETA
      IF(THET.GT.90.) THET=180.-THET
      PT=P*SIN(THETA)
      IF(THET.LT.40.AND.THET.GT.5.) THEN ! ENDS;USE MOMENTUM
        IP=P
        IF(IP.GT.16) THEN
          EFF=EFFP(18)
        ELSE
          DEL=P-IP
          EFF=EFFP(IP+1)+(EFFP(IP+2)-EFFP(IP+1))*DEL
        ENDIF
      ELSE IF(THET.GT.40.) THEN          ! CENTRAL;USE PT
        IP=PT
        IF(IP.GT.9) THEN
          EFF=EFFPT(11)
        ELSE
          DEL=PT-IP
          EFF=EFFPT(IP+1)+(EFFPT(IP+2)-EFFPT(IP+1))*DEL
        ENDIF
      ELSE IF(THET.LT.5.) THEN
        EFF=0.
      ENDIF
      IF(RANF().LT.EFF) ITRIG=ITRIG+2
      RETURN
      END
