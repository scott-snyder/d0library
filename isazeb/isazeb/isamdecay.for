      SUBROUTINE ISAMDECAY(IDENT,XAIR,P,E,IDEC,PRODEC,EMUON,ANGMU)
CC SIMPLE DECAY ROUTINE
CC SEES IF PI +-, K+-, OR KL DECAYS INTO MUON
CC IDEC=1 THEN DECAY TO MU OCCURED
CC PRODECIS PROBABILITY OF DECAY
CC  DOES DECAY KINEMATICS BUT IGNORES MU DIRECTION FOR NOW
CC   UPDATED DH 5-87---> ISAMDECAY 11/90 DH
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT NONE
      DOUBLE PRECISION ONE,PROBE,PRDEL,PROB
      REAL XAIR,P,E,PRODEC,EMUON,ANGMU,EMMU,CTAUPI,CTAUKL,CTAUKP,
     A BRMUPI,BRMUKL,BRMUKP,PM(3),DIR(3),AMASS,CTAU,BRMU,GAMMA,RANF
      REAL BETA,ENU,PNU,PPI,COST,SINT,PHIM,PMUON,EMU,PMU,EPI
      INTEGER IDENT,IDEC,NBODY
CC
      DATA ONE/1.00/
      DATA EMMU/.10566/
      DATA CTAUPI,CTAUKL,CTAUKP/7.8,15.5,3.7/
      DATA BRMUPI,BRMUKL,BRMUKP/1.,.27,.667/
CC
      PRODEC=0.
      IDEC=0
      EMUON=0.
      CTAU=0.
      IF(IABS(IDENT).EQ.120) THEN
CC PI
        AMASS=.139
        CTAU=CTAUPI
        BRMU=BRMUPI
        NBODY=2.
      ENDIF
      IF(IABS(IDENT).EQ.130) THEN
CC K+ 
        AMASS=.494
        CTAU=CTAUKP
        BRMU=BRMUKP
        NBODY=2.
      ENDIF
      IF(IDENT.EQ.-20) THEN
CC KL
        AMASS=.498
        CTAU=CTAUKL
        BRMU=BRMUKL
        NBODY=3.
      ENDIF
      IF(CTAU.EQ.0.) RETURN
      GAMMA=E/AMASS
      IF(GAMMA.LE.1.) GAMMA=1.000001
        PROB=XAIR/CTAU/GAMMA
      PROBE=EXP(-PROB)
      PRDEL=ONE-PROBE
      PRODEC=BRMU*PRDEL
CC  SEE IF DECAY INTO MU
      IF(RANF().LT.PRODEC) IDEC=1
CCC GET MU ENERGY
CC FIRST IN C. OF M.
      IF(NBODY.EQ.2) CALL ISAMTWOBOD(AMASS,EMMU,0.,EMU,ENU,PMU,PNU)
      IF(NBODY.EQ.3) CALL ISAMKMU3(EPI,EMU,PPI,PMU)
CC  RANDOM ANGLE IN CM
      COST=-1.+2.*RANF()
      SINT=SQRT(1.-COST**2)
      PHIM=2.*3.14159*RANF()
      PM(1)=PMU*SINT*SIN(PHIM)
      PM(2)=PMU*SINT*COS(PHIM)
      PM(3)=PMU*COST
CC PI FORWARD
      DIR(1)=0.
      DIR(2)=0.
      DIR(3)=1.
      BETA=SQRT((GAMMA**2-1.)/GAMMA**2)
CC  TRANSFORM TO LAB
      CALL ISAMLORTRN(BETA,GAMMA,DIR,PM,EMU,PMU,EMMU)
CC  SAVE MU ENERGY IGNORE DIRECTION FOR NOW
      EMUON=EMU
      PMUON=PMU
      ANGMU=ACOS(DIR(3))
      RETURN
      END
